readDataFrom: aDataStream size: anInteger
    "Create an object based on the contents of aDataStream, which was
       generated by the objectÕs storeDataOn: method. Answer it.
     This implementation reads back the instance-variables written by
       Object>>storeDataOn:.
     NOTE: This method must match its corresponding storeDataOn:
       method. Also, it must send beginReference: after instantiating
       the new object but before reading any objects from aDataStream
       that might reference it.
     11/23/92 jhm: Changed to basicNew & basicNew: to match basicSize and because
        we then overwrite all instance & indexed vars."
    | anObject cntInstVars cntIndexedVars |

    cntInstVars _ self instSize.
    anObject _ self isVariable
        ifTrue:  [cntIndexedVars _ anInteger - cntInstVars.
                 self basicNew: cntIndexedVars]
        ifFalse: [cntIndexedVars _ 0.
                 self basicNew].

    aDataStream beginReference: anObject.
    1 to: cntInstVars do:
        [:i | anObject instVarAt: i put: aDataStream next].
    1 to: cntIndexedVars do:
        [:i | anObject basicAt: i put: aDataStream next].
"self == Association ifTrue: [
	anObject value == Obj homeObject ifTrue: [self halt]]."

    ^ anObject